/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.6.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.9.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.5.0'
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:4.50.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.6.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.4.0'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.5.3'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'

repositories {
    mavenCentral()
}

allprojects {
    apply plugin: 'com.palantir.java-format'
    version System.env.CIRCLE_TAG ?: gitVersion()
    group 'com.palantir.tracing'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: "org.inferred.processors"

    repositories {
        mavenCentral()
    }

    tasks.check.dependsOn(javadoc)

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:deprecation', '-Werror']
    }

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            compileOnly 'com.google.code.findbugs:jsr305'
            annotationProcessor 'com.uber.nullaway:nullaway'
        }

        tasks.withType(JavaCompile) {
            options.errorprone.errorproneArgs.add '-XepOpt:NullAway:AnnotatedPackages=com.palantir'
        }
    })
}

javaVersions {
    libraryTarget = 11
    runtime = 17
}
