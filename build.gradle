/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'https://palantir.bintray.com/releases'}
    }

    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:1.0.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.4.3'
        classpath 'com.netflix.nebula:gradle-info-plugin:8.3.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.3.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:3.1.1'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.23.0'
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.4.7"
    }
}

apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'

repositories {
    jcenter()
    maven { url  'https://palantir.bintray.com/releases'}
}

allprojects {
    apply plugin: 'com.palantir.java-format'
    version System.env.CIRCLE_TAG ?: gitVersion()
    group 'com.palantir.tracing'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: "org.inferred.processors"

    repositories {
        jcenter()
        maven { url 'https://palantir.bintray.com/releases'}
    }

    sourceCompatibility = 1.8
    tasks.check.dependsOn(javadoc)

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:deprecation', '-Werror']
    }

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            compileOnly 'com.google.code.findbugs:jsr305'
            annotationProcessor 'com.uber.nullaway:nullaway'
        }

        tasks.withType(JavaCompile) {
            options.errorprone.errorproneArgs += ['-XepOpt:NullAway:AnnotatedPackages=com.palantir']
        }
    })
}
