/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
        gradlePluginPortal() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    dependencies {
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.25.0'
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.2.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.11.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.26.0'
        classpath 'com.palantir.gradle.revapi:gradle-revapi:1.7.0'
        classpath 'gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:4.161.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.15.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.6.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.11.0'
        classpath 'me.champeau.gradle:jmh-gradle-plugin:0.5.3'
    }
}

apply plugin: 'com.palantir.external-publish'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

repositories {
    mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
}

allprojects {
    apply plugin: 'com.palantir.java-format'
    version System.env.CIRCLE_TAG ?: gitVersion()
    group 'com.palantir.tracing'
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: "org.inferred.processors"

    repositories {
        mavenCentral() { metadataSources { mavenPom(); ignoreGradleMetadataRedirection() } }
    }

    tasks.check.dependsOn(javadoc)

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:deprecation', '-Werror']
    }

    plugins.withId('com.palantir.baseline-error-prone', {
        dependencies {
            compileOnly 'com.google.code.findbugs:jsr305'
            annotationProcessor 'com.uber.nullaway:nullaway'
        }

        tasks.withType(JavaCompile) {
            options.errorprone.errorproneArgs.add '-XepOpt:NullAway:AnnotatedPackages=com.palantir'
        }
    })

    dependencies {
        modules {
            module('org.glassfish.hk2.external:javax.inject') {
                replacedBy("javax.inject:javax.inject", "javax version is preferred")
            }
            module('jakarta.ws.rs:jakarta.ws.rs-api') {
                replacedBy("javax.ws.rs:javax.ws.rs-api", "javax version is preferred (for now, until jakarta packages are prevalent)")
            }
            module('org.jboss.spec.javax.annotation:jboss-annotations-api_1.3_spec') {
                replacedBy("javax.annotation:javax.annotation-api", "javax version is preferred")
            }
        }
    }
}

javaVersions {
    libraryTarget = 11
    runtime = 17
}
