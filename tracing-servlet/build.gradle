/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.palantir.external-publish-jar'
apply plugin: 'com.palantir.revapi'

versionsLock {
    disableJavaPluginDefaults()
}

dependencies {
    // tracing is used to clear tracing state, but not exposed to other consumers
    implementation project(":tracing")

    // servlet-api should be the only exposed dependency
    api "javax.servlet:javax.servlet-api", {
        version {
            strictly '4.0.1'
        }
    }

    implementation 'com.palantir.safe-logging:logger'

    testImplementation "io.dropwizard:dropwizard-testing", {
        version {
            strictly '2.0.10'
        }
    }
    testImplementation "junit:junit"
    testImplementation 'org.assertj:assertj-core'

}

configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group.startsWith('org.glassfish.jersey')) {
            details.useVersion '2.22.2'
            details.because 'forcing the legacy javax version'
        }
        if (details.requested.group == 'org.glassfish.jersey.inject') {
            details.useVersion '2.26'
            details.because 'forcing the legacy javax version'
        }
    }
}